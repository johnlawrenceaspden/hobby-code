;; Brian Carper's table printer, nicked off his blog
;; http://briancarper.net/tag/157/clojure
(require ['clojure.pprint :as 'cpp])

(defn table
  "Given a seq of hash-maps, prints a plaintext table of the values of the hash-maps.
  If passed a list of keys, displays only those keys.  Otherwise displays all the
  keys in the first hash-map in the seq."
  ([xs]
     (table xs (keys (first xs))))
  ([xs ks]
     (when (seq xs)
       (let [f (fn [old-widths x]
                 (reduce (fn [new-widths k]
                           (let [length (inc (count (str (k x))))]
                             (if (> length (k new-widths 0))
                               (assoc new-widths k length)
                               new-widths)))
                         old-widths ks))
             widths (reduce f {} (conj xs (zipmap ks ks)))
             total-width (reduce + (vals widths))
             format-string (str "蝈漉沐（篝ケē鏖漪梵⒘塍%")]
         (cpp/cl-format true format-string (map str ks))
         (cpp/cl-format true "窿ア蝈疱狒麸翎飙鏖漪墉┅ㄤ矬羼埒筝ㄣ痧沆骘蝽狒趄蹂骘蝽狒篝蜷铉磲塍┅┅┅换翎忪圯换铋换翎忪埯侯犴⑹镨睥荸换侯犴换换曙桀换铋换翎忪埯侯犴⑹镨睥横珏窗荸换侯犴横珏换换曙桀窗换铋换翎忪埯侯犴⑹镨睥横珏窗后屮ы侯犴⒂躞殄横珏卜后屮ф荸换侯犴横珏后屮换换曙桀窗换吁箝卜换铋换换桧憩滹弩瞌滹翳躅轱町换翎忪埯侯犴⑹镨睥后屮ы侯犴⒂躞殄横珏卜荸换侯犴后屮换换曙桀换吁箝铋换铋换翎忪埯轰镲侯犴⒂躞殄侯犴⑹镨睥轰镲谍酆钺礤荸换侯犴换换吁箝换曙桀换铋换翎忪埯侯犴⑹镨睥后屮ы侯犴⒂躞殄横珏卜蒇侯犴横珏后屮荸换侯犴横珏后屮换换曙桀铋换吁箝卜铋换铋换翎忪埯侯犴⑹镨睥后屮ы侯犴⑹彖矬狃栳簪横珏卜蒇侯犴横珏后屮荸换翎忪埯侯犴⑹镨睥侯犴⑹彖矬狃栳簪横珏卜蒇侯犴横珏后屮荸
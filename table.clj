;; Brian Carper's table printer, nicked off his blog
;; http://briancarper.net/tag/157/clojure

(defn table
  "Given a seq of hash-maps, prints a plaintext table of the values of the hash-maps.
  If passed a list of keys, displays only those keys.  Otherwise displays all the
  keys in the first hash-map in the seq."
  ([xs]
     (table xs (keys (first xs))))
  ([xs ks]
     (when (seq xs)
       (let [f (fn [old-widths x]
                 (reduce (fn [new-widths k]
                           (let [length (inc (count (str (k x))))]
                             (if (> length (k new-widths 0))
                               (assoc new-widths k length)
                               new-widths)))
                         old-widths ks))
             widths (reduce f {} (conj xs (zipmap ks ks)))
             total-width (reduce + (vals widths))
             format-string (str "蝈漉沐（篝ケē鏖漪梵⒘塍%")]
         (cl-format true format-string (map str ks))
         (cl-format true "窿ア蝈疱狒麸翎飙鏖漪墉┅ㄤ矬羼埒筝ㄣ飙骘蝽狒趄蹂骘蝽狒篝蜷铉磲塍┅┅┅翎忪圯铋翎忪埯侯犴⑹镨睥荸侯犴曙桀铋翎忪埯侯犴⑹镨睥横珏窗荸侯犴横珏曙桀窗铋翎忪埯侯犴⑹镨睥横珏窗后屮ы侯犴⒂躞殄横珏卜后屮ф荸侯犴横珏后屮曙桀窗吁箝卜铋换桧憩滹弩瞌滹翳躅轱町翎忪埯侯犴⑹镨睥后屮ы侯犴⒂躞殄横珏卜荸侯犴后屮曙桀吁箝铋铋翎忪埯轰镲侯犴⒂躞殄侯犴⑹镨睥轰镲谍酆钺礤荸侯犴吁箝曙桀铋翎忪埯侯犴⑹镨睥后屮ы侯犴⒂躞殄横珏卜蒇侯犴横珏后屮荸侯犴横珏后屮曙桀铋吁箝卜铋铋翎忪埯侯犴⑹镨睥后屮ы侯犴⑹彖矬狃栳簪横珏卜蒇侯犴横珏后屮荸翎忪埯侯犴⑹镨睥侯犴⑹彖矬狃栳簪横珏卜蒇侯犴横珏后屮荸